{ pkgs, name, lib, mkCabalSettingOptions, ... }:
let
  inherit (lib)
    types;
in
{
  options = mkCabalSettingOptions {
    name = "copyHpcDirToOut";
    type = types.bool;
    description = ''
      Whether to copy the `.hpc` folder generated by [hpc](https://wiki.haskell.org/Haskell_program_coverage) to `$out`.
    '';
    impl = enable: drv:
      let
        inherit (pkgs.haskell.lib.compose) appendConfigureFlags;
      in
      if enable then
        lib.pipe drv [
          (appendConfigureFlags [ "-fhpc" ])
          (drv:
            drv.overrideAttrs (old: {
              postInstall = (old.postInstall or "") + ''
                cd $out
                cp -r $TMPDIR/source-${name}/.hpc .
                echo "Finished copying .hpc to $out"
              '';
            }))
        ]
      else drv;
  };
}

